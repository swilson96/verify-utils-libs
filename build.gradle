apply plugin: 'idea'
apply plugin: 'java'
apply from: 'sonar.gradle'

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
    }
    module {
        //if you love browsing Javadoc
        downloadJavadoc = true

        //and hate reading sources :)
        downloadSources = true
    }
}

buildscript {
    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
    dependencies {
        classpath 'uk.gov.ida:ida-gradle:1.0.0-4'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idaJar'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = "uk.gov.ida"
    version = "1.0.0-$System.env.BUILD_NUMBER"

    repositories {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }


    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }

            repositories {
                maven {
                    url "/srv/maven" // change to point to your repo, e.g. http://my.org/repo
                }
            }

        }
    }
}

task sonar(dependsOn: [':clean', ':build', 'sonarRunner'])
